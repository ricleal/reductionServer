%class Handler
%package handler

%start MainMap::Idle

%map MainMap
%%

// Note:
// Transition actions can't have a return type!!!

// accepting new files
Idle
Entry { setStatus('Idle'); }
{   
	//Trans 
 	receiveFile(request)
 		//Next
 		ProcessingFile 	 
	        // Actions
    		{ 
    			processFile(request);
    			setStatus('Processing input file'); 
    		}
}

ProcessingFile 
Entry { checkFileProcessing(); }
{
	//Trans //Next // Actions  
 	success Waiting { setSuccessStatus('File successfully processed.'); }
	failure Idle { setErrorStatus('ERROR: File NOT processed.'); }
}

Waiting
Entry { setStatus('Waiting for queries'); }
{
	query(content) ProcessingQuery { handleQuery(content); }
	result(content) Waiting { handleResult(content); } 
	reset Idle { cleanUp(); }
}

ProcessingQuery
Entry { setStatus('Processing query'); }
{
	//Trans //Next // Actions  
 	success Waiting { setSuccessStatus('Query satisfied'); }
	failure Waiting { setErrorStatus('ERROR: Query NOT satisfied'); }
	timeout Waiting { setErrorStatus('ERROR: Timeout!'); }
}

%%
